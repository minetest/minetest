buildscript {
	repositories {
		mavenCentral()
		jcenter()
		google()
	}
	dependencies {
		classpath "com.android.tools.build:gradle:3.0.1"
	}
}

apply plugin: "com.android.application"

android {
	compileSdkVersion 25
	buildToolsVersion "26.0.2"

	defaultConfig {
		versionCode 17
		versionName "${System.env.VERSION_STR}.${versionCode}"
		minSdkVersion 14
		targetSdkVersion 14
		applicationId "net.minetest.minetest"
		manifestPlaceholders = [ package: "net.minetest.minetest", project: project.name ]
		ndk {
			// Specifies the ABI configurations of your native
			// libraries Gradle should build and package with your APK.
			abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a',
			'arm64-v8a'
		}
	}

	lintOptions {
		disable "OldTargetApi", "GoogleAppIndexingWarning"
	}

	Properties props = new Properties()
	props.load(new FileInputStream(file("local.properties")))

	if (props.getProperty("keystore") != null) {
		signingConfigs {
			release {
				storeFile file(props["keystore"])
				storePassword props["keystore.password"]
				keyAlias props["key"]
				keyPassword props["key.password"]
			}
		}

		buildTypes {
			release {
				signingConfig signingConfigs.release
			}
		}
	}
}
